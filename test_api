#!/usr/bin/env perl
use warnings;
use strict;
#
# Quick commandline tool to call each cloudatcost API entry point
#

# allow the libs to be in the bin dir
use FindBin;
use lib "$FindBin::RealBin/lib";
use lib "/home/hamish/s/bin/lib";

use Getopt::Long 2.33 qw(:config gnu_getopt);
use Pod::Usage;

use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Quotekeys = 0;

use HC::CredentialStore;
use HC::Cache::Dir;
use REST::JSONRequest;
use CloudAtCost;


my $option = {
    preset   => 'cac',
    verbose  => 0,
};
my @option_list = (
    "preset|s=s",
    "credfile=s",
    "verbose|v+",
    "cachedir=s",
    "help",
);

###############################################################################
##
## Standard options processor
sub do_options {
    my $option = shift;
    if (!GetOptions($option,@_)) {
        print("Options: ",join(", ",@_),"\n");
        pod2usage(2);
    }
    if ($option->{man}) {
        pod2usage(-exitstatus => 0, -verbose => 2);
    }

    if ($option->{quiet}) {
        delete $option->{verbose};
    }
}

###############################################################################

# FIXME - should this logic move into the cache package?
sub calculate_cachedir {
    if (defined($option->{cachedir})) {
        # manually setting it on the cmdline takes precedence
        return $option->{cachedir};
    }

    # finally, fall back to something in our home directory
    return $ENV{'HOME'}.'/.cache/cloudatcost';
}

sub render_table {
    my $columns = shift;
    my $table = shift;
    my $grid;

    push @{$grid}, $columns;
    for my $row (@{$table}) {
        my $tmp;
        for my $column (@{$columns}) {
            push @{$tmp}, $row->{$column} ||'';
        }
        push @{$grid},$tmp;
    }

    my @col_widths;
    for my $row (@{$grid}) {
        for my $col (0..scalar(@{$columns})-1) {
            my $this_len = length($row->[$col]) ||0;
            my $col_width = $col_widths[$col]||0;
            if ($this_len > $col_width) {
                $col_widths[$col] = length($row->[$col])
            }
        }
    }

    my @s;
    for my $row (@{$grid}) {
        for my $col (0..scalar(@{$columns})-1) {
            push @s, sprintf("%*s ",$col_widths[$col],$row->[$col]);
        }
        push @s, "\n";
    }
    return join('',@s);
}

sub render_error {
    my $service = shift;
    my $s;

    if ($option->{verbose}) {
        $s .= "\n".Dumper($service)."\n";
    }

    $s .= "error: ".$service->error()." ".$service->error_description."\n";
    return $s;
}

sub cmd_listservers {
    my $service = shift;
    my $servers = $service->listservers();
    return render_error($service) if (!defined($servers));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($servers)."\n";
    }

    my @columns = qw(sid hostname ip cpu ram storage mode status);
    $s .= render_table(\@columns, $servers);

    return $s;
}

sub cmd_listtemplates {
    my $service = shift;
    my $templates = $service->listtemplates();
    return render_error($service) if (!defined($templates));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($templates)."\n";
    }

    my @columns = qw(ce_id name);
    $s .= render_table(\@columns, $templates);

    return $s;
}

sub cmd_listtasks {
    my $service = shift;
    my $result = $service->listtasks();
    return render_error($service) if (!defined($result));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }

    my @columns = qw(cid starttime serverid action status servername);
    $s .= render_table(\@columns, $result);

    return $s;
}

sub cmd_powerop {
    my $service = shift;
    my $sid = shift;
    my $action = shift;
    die("need sid") if (!defined($sid));
    die("need action") if (!defined($action));
    my $result = $service->powerop($sid,$action);
    return render_error($service) if (!defined($result));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    $s.="OK\n";
    return $s;
}

sub cmd_runmode {
    my $service = shift;
    my $sid = shift;
    my $mode = shift;
    die("need sid") if (!defined($sid));
    die("need mode") if (!defined($mode));
    my $result = $service->runmode($sid,$mode);
    return render_error($service) if (!defined($result));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    $s.="OK\n";
    return $s;
}

sub cmd_renameserver {
    my $service = shift;
    my $sid = shift;
    my $name = shift;
    die("need sid") if (!defined($sid));
    die("need name") if (!defined($name));
    my $result = $service->renameserver($sid,$name);
    return render_error($service) if (!defined($result));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    $s.="OK\n";
    return $s;
}

sub cmd_rdns {
    my $service = shift;
    my $sid = shift;
    my $hostname = shift;
    die("need sid") if (!defined($sid));
    die("need hostname") if (!defined($hostname));
    my $result = $service->rdns($sid,$hostname);
    return render_error($service) if (!defined($result));
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    $s.="OK\n";
    return $s;
}

sub cmd_console {
    my $service = shift;
    my $sid = shift;
    die("need sid") if (!defined($sid));
    my $result = $service->console($sid);
    return render_error($service) if (!defined($result));
    my $s = '';

    $result = $service->prev_full_result();
    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    if (defined($result->{console})) {
        $s.=$result->{console}."\n";
    } else {
        $s.="wierd\n";
    }
    return $s;
}

sub cmd_build {
    my $service = shift;
    my %fields;
    for my $param (@_) {
        my ($key,$val) = split('=',$param);
        $fields{$key} = $val;
    }
    my $result = $service->build(%fields);
    return render_error($service) if (!defined($result));
    my $s = '';

    $result = $service->prev_full_result();
    if ($option->{verbose}) {
        $s .= "\n".Dumper($result)."\n";
    }
    if (defined($result->{taskid})) {
        $s.=$result->{taskid}."\n";
    } else {
        $s.="wierd\n";
    }
    return $s;
}

sub cmd_resources {
    my $service = shift;
    my $resources = $service->resources();
    my $s = '';

    if ($option->{verbose}) {
        $s .= "\n".Dumper($resources)."\n";
    }

    my $table;
    $table->[0] = {
        type    => 'used',
        cpu     => $resources->{used}{cpu_used},
        ram     => $resources->{used}{ram_used},
        storage => $resources->{used}{storage_used},
    };
    $table->[1] = {
        type    => 'total',
        cpu     => $resources->{total}{cpu_total},
        ram     => $resources->{total}{ram_total},
        storage => $resources->{total}{storage_total},
    };

    my @columns = qw(type cpu ram storage);
    $s .= render_table(\@columns, $table);

    return $s;
}

sub cmd_fixme {
    die "FIXME";
}

my $cmds = {
    'listservers'   => {
        cmd  => \&cmd_listservers,
        help => 'List all servers on the account',
    },
    'listtemplates' => {
        cmd  => \&cmd_listtemplates,
        help => 'List all templates available',
    },
    'listtasks' => {
        cmd  => \&cmd_listtasks,
        help => 'List all tasks in operation',
    },
    'powerop' => {
        cmd  => \&cmd_powerop,
        help => 'Activate server power operations',
    },
    'runmode' => {
        cmd  => \&cmd_runmode,
        help => 'Set the run mode of the server',
    },
    'renameserver' => {
        cmd  => \&cmd_renameserver,
        help => 'Rename the server label',
    },
    'rdns' => {
        cmd  => \&cmd_rdns,
        help => 'Modify the reverse DNS & hostname of the VPS',
    },
    'console' => {
        cmd  => \&cmd_console,
        help => 'Request URL for console access',
    },
    'cloudpro/build' => {
        cmd  => \&cmd_build,
        help => 'Build a server from available resources',
    },
    'cloudpro/delete' => {
        cmd  => \&cmd_fixme,
        help => 'Delete / terminate server to add resources',
    },
    'cloudpro/resources' => {
        cmd  => \&cmd_resources,
        help => 'Display resources available and resources used in cloud-pro',
    },
};

sub main() { 
    do_options($option,@option_list);
    if (defined($option->{help})) {
        print("Sub commands:\n\n");
        for my $cmd (sort(keys(%{$cmds}))) {
            printf("%-18s %s\n",$cmd,$cmds->{$cmd}{help});
        }
        return;
    }

    my ($username,$key);
    if (defined($option->{credfile})) {
        my $cred = HC::CredentialStore->new($option->{credfile});
        die("credfile error") if (!defined($cred));

        ($username,$key) = $cred->lookup($option->{preset});
    }

    die "Need username credentials" if (!defined($username));
    die "Need key credentials" if (!defined($key));

    my $client = REST::JSONRequest->new();
    $client->set_urlprefix('https://panel.cloudatcost.com/');

    my $cache = HC::Cache::Dir->new();
    $cache->set_cachedir(calculate_cachedir());

    my $cloudatcost = CloudAtCost->new();
    $cloudatcost->set_Request($client);
    $cloudatcost->set_Cache($cache);
    $cloudatcost->set_credentials($username,$key);

    my $cmd = shift(@ARGV);
    if (!defined($cmd) || !defined($cmds->{$cmd})) {
        die('No such sub-command');
    }

    my $result = $cmds->{$cmd}{'cmd'}($cloudatcost,@ARGV);
    print $result;
}
main();

